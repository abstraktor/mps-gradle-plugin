plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
}

group 'de.itemis.mps'
version de.itemis.mps.gradle.GitBasedVersioning.getVersion(1, 0)

task wrapper(type: Wrapper) {
  gradleVersion '3.4.1'
  distributionType 'all'
}

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
}

ext.repository = version.toString().endsWith('SNAPSHOT') ?
        'https://projects.itemis.de/nexus/content/repositories/mbeddr_snapshots' :
        'https://projects.itemis.de/nexus/content/repositories/mbeddr'

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url project.repository
            if (project.hasProperty('nexusUsername')) {
                credentials {
                    username project.nexusUsername
                    password project.nexusPassword
                }
            }
        }
    }
}
